plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'com.security'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    implementation group: 'org.ow2.asm', name: 'asm-tree', version: '9.6'
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.6'
    implementation group: 'io.netty', name: 'netty-all', version: '4.1.85.Final'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'com.lmax', name: 'disruptor', version: '3.4.4'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.14'
    implementation 'org.lz4:lz4-java:1.8.0'
    // implementation 'org.openjdk.nashorn:nashorn-core:15.0'
    api group: 'org.javassist', name: 'javassist', version: '3.29.0-GA'
}

compileJava {
    options.release.set(8)
}

jar {
    manifest {
        attributes 'Agent-Class': 'com.security.smith.SmithAgent'
        attributes 'Premain-Class': 'com.security.smith.SmithAgent'
        attributes 'Can-Retransform-Classes': 'true'
        attributes 'Boot-Class-Path': 'SmithAgent.jar'
        attributes 'Specification-Title': 'Smith Agent'
        attributes 'Specification-Version': '1.0'
        attributes 'Implementation-Title': 'Smith Agent'
        attributes 'Implementation-Version': "$System.env.BUILD_VERSION"
    }
}

shadowJar {
    relocate 'io.netty', 'rasp.io.netty'
    relocate 'org.objectweb.asm', 'rasp.org.objectweb.asm'
    relocate 'org.apache.commons', 'rasp.org.apache.commons'
    relocate 'org.yaml.snakeyaml', 'rasp.org.yaml.snakeyaml'
    relocate 'com.lmax.disruptor', 'rasp.com.lmax.disruptor'
    relocate 'com.google','rasp.com.google'
    relocate 'com.esotericsoftware', 'rasp.com.esotericsoftware'
   // relocate 'org.openjdk.nashorn', 'org.openjdk.nashorn'
    relocate 'javassist', 'rasp.javassist'
    relocate 'net.jpountz', 'rasp.net.jpountz'
    relocate 'META-INF/native/libnetty', 'META-INF/native/librasp_netty'
}